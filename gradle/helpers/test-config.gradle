apply from: 'gradle/helpers/test-printing.gradle'

tasks.withType(Test).all {
    systemProperty "spring.profiles.active", "dev"
    testLogging.events = ['standard_out', 'standard_error']
}

task unit (type: Test) {
    testLogging { afterSuite { desc, result -> printTestResults(desc,result,'UNIT') } }
    exclude '**/cucumber/**'
}

task cucumber (type: Test) {
    testLogging { afterSuite { desc, result -> printTestResults(desc,result,'CUKE') } }
    include '**/cucumber/**'
    testLogging.events = ['standard_out']

    // Use for debugging specific Cucumber tests
    // Add the @debug tag above a feature or scenario you want to test
    //systemProperty "cucumber.options", "--tags @debug"
}

// If not running in Travis, we want to use the unit task instead of test
if (System.env.TRAVIS != 'true') {
    test { exclude '**' }
    test.dependsOn unit
}

// For running all tests during build
if(System.env.TESTS == 'ALL') {
  test.dependsOn cucumber
}

cucumber.mustRunAfter unit
